artdb:
REM * RACCORD. CIRCULAIRE *
REM * DGX * DEC.90 *
LOCAL AS,B,C,DEV,D1,D2,FL,TG,X1,X2,X3,X4,X5,X6,x7,X8,X9,X10,Y1,Y2,Y3,Y4,Y5,Y6,Y7,y8,Y9,Y10,DX1,DX2,DX3,DY1,DY2,DY3,DXX,DYY,DXT,DYT,G1,G2,G3,g4,GIS1,GIS2,T%,M%,ray,p1,p2,p$(1),sec,sc
IF PEEKB($2099)<>255 :RETURN :ENDIF
BEEP 50,200 :PAUSE 15 :BEEP 50,200
AT 3,1 :PRINT "CARACTERISTIQUES"
AT 2,2 :PRINT "D";CHR$(39);"UN  RACCORDEMENT"
AT 6,3 :PRINT "CIRCULAIRE"
VIEW(4,"***   GUILLOUX  Daniel - 06100 Nice - ")
DEBUT::
CLS :BEEP 50,100
AT 1,1 :PRINT "X Pt1 Dte1";chr$(63); :input X1
IF X1=0 :GOTO MENU:: :ENDIF
AT 1,2 :PRINT "Y Pt1 Dte1";chr$(63); :INPUT Y1
AT 1,3 :PRINT "Gis. 1 Sinon 0";CHR$(63); :INPUT G1
IF G1>400 :G1=G1-400 :ENDIF
IF G1=0 :AT 1,3 :PRINT "X Pt2 Dte1 ";chr$(63); :input x2
AT 1,4 :PRINT "Y Pt2 Dte1";chr$(63); :INPUT Y2
ELSEIF G1<>0 :GIS1=G1*PI/200 :GOTO D2::
ENDIF
DX1=X1-X2 :DY1=Y1-Y2
IF DY1=0 :DY1=.000000000001
ENDIF
GIS1=ATAN(DX1/DY1)
G1=GIS1/PI*200
IF DY1>0 :G1=G1+200
ELSEIF DX1>0 :G1=G1+400
ENDIF
D2:: :CLS :BEEP 50,100
AT 1,1 :PRINT "X Pt1 Dte2 ";CHR$(63); :INPUT X3
IF X3=0 :GOTO MENU:: :ENDIF
AT 1,2 :PRINT "Y Pt1 Dte2 ";chr$(63); :INPUT Y3
AT 1,3 :PRINT "Gis. 2 Sinon 0";CHR$(63); :INPUT G2
IF G2>400 :G2=G2-400 :ENDIF
IF G2=0 :AT 1,3 :PRINT "X Pt2 Dte2 ";CHR$(63); :INPUT X4
AT 1,4 :PRINT "Y Pt2 Dte2 ";chr$(63); :INPUT Y4
ELSEIF G2<>0 :GIS2=G2*PI/200 :GOTO CERCLE::
ENDIF
DX2=X3-X4 :DY2=Y3-Y4
IF DY2=0 :DY2=.000000000001
ENDIF
GIS2=ATAN(DX2/DY2)
G2=GIS2/PI*200
IF DY2>0 :G2=G2+200
ELSEIF DX2>0 :G2=G2+400
ENDIF
CERCLE:: :CLS :BEEP 50,100 :kstat 1
at 1,1 :print "RAYON du Cercle ";chr$(63); :input ray
IF RAY=0 :GOTO MENU:: :ENDIF
at 2,2 :PRINT "Pos. Cercle Drte 1 "
at 2,3 :print "G=Gauche  D=Droite"
pos1:: :beep 50,100
at 10,4 :print chr$(63); :input p$
if p$="G" :p1=-1 
elseif p$="D" :p1=1
else :goto pos1::
endif
at 2,2 :PRINT "Pos. Cercle Drte 2"
at 2,3 :print "G=Gauche  D=Droite"
pos2:: :beep 50,100
at 10,4 :print chr$(63); :input p$
if p$="G" :p2=-1 
elseif p$="D" :p2=1
else :goto pos2::
endif
IF G1=400 :G1=0
ELSEIF G2=400 :G2=0 :ENDIF 
CLS :BEEP 50,100
PRINT "Droite 1-> Gisement"
print "    ";FIX$(G1,4,-10)
PRINT "Droite 2-> Gisement"
PRINT "    ";FIX$(G2,4,-10)
get
cls :BEEP 50,100
AT 1,1 :PRINT "RACCORD.  CIRCULAIRE"
AT 1,2 :PRINT "--------------------"
at 1,3 :print " CALCULS EN COURS.."
at 1,4 :print "    Patience....."
IF G1=G2 OR G1=G2+200 OR G2=G1+200 :CLS :PRINT CHR$(16): :AT 6,1 :PRINT "IMPOSSIBLE" :PRINT " DROITES PARALLELES          OU              CONFONDUES" :PAUSE 100 :GOTO MENU:: :ENDIF
DYY=X1-X3-TAN(GIS2)*(Y1-Y3)
DYT=DYY/(TAN(GIS2)-TAN(GIS1))
DXT=DYT*TAN(GIS1)
X5=X1+DXT :Y5=Y1+DYT
D1=SQR((DXT*DXT)+(DYT*DYT))
DX3=X5-X3 :DY3=Y5-Y3
D2=SQR((DX3*DX3)+(DY3*DY3))
rem centre cercle
g3=g1+100 :g3=g3*PI/200
g4=g2+100 :g4=g4*PI/200
x6=x1+(sin(g3)*(ray*p1))
y6=y1+(cos(g3)*(ray*p1))
x7=x3+(sin(g4)*(ray*p2))
y7=y3+(cos(g4)*(ray*p2))
DYY=X6-X7-TAN(GIS2)*(Y6-Y7)
DYT=DYY/(TAN(Gis2)-TAN(Gis1))
DXT=DYT*TAN(Gis1)
X8=X6+DXT :Y8=Y6+DYT
rem TG.1
DYY=X1-X8-TAN(G3)*(Y1-Y8)
DYT=DYY/(TAN(G3)-TAN(Gis1))
DXT=DYT*TAN(Gis1)
X9=X1+DXT :Y9=Y1+DYT
REM TG.2
DYY=X3-X8-TAN(G4)*(Y3-Y8)
DYT=DYY/(TAN(G4)-TAN(Gis2))
DXT=DYT*TAN(Gis2)
X10=X3+DXT :Y10=Y3+DYT
rem Long. TG.
DX3=X5-X9 :DY3=Y5-Y9
tg=SQR((DX3*DX3)+(DY3*DY3))
REM CENTRE/SOMMET
DX3=X5-X8 :DY3=Y5-Y8
sc=SQR((DX3*DX3)+(DY3*DY3))
rem Corde
DX3=X9-X10 :DY3=Y9-Y10
c=SQR((DX3*DX3)+(DY3*DY3))
rem Bissectrice
DX3=X8-X5 :DY3=Y8-Y5
B=SQR((DX3*DX3)+(DY3*DY3))
b=b-ray
rem Angl sommet
as=atan(tg/ray)
REM FLECHE
FL=ray*(1-cos(as))
REM DEVELOP.
DEV=PI*ray*as/(100*pi/200)
rem Secteur
sec=pi*(ray*ray)*as/PI
REM ANG. SOM.
AS=PI-2*AS
AS=AS/PI*200
BEEP 50,200 :pause 15 :BEEP 50,200 :CLS
AT 1,1 :PRINT "X Sommet ";FIX$(X5,4,-11)
AT 1,2 :PRINT "Y Sommet ";FIX$(Y5,4,-11)
AT 1,3 :PRINT "Dist Dte1";fix$(D1,4,-11)
AT 1,4 :PRINT "Dist Dte2";fix$(D2,4,-11)
GET :BEEP 50,200
AT 1,3 :PRINT REPT$(" ",40)
AT 1,3 :PRINT chr$(194);" Sommet ";fix$(as,4,-11)
AT 1,4 :PRINT "Long. Tang";fix$(tg,4,-10)
get :cls :beep 50,200
AT 1,1 :PRINT "X Tang.1 ";FIX$(X9,4,-11)
AT 1,2 :PRINT "Y Tang.1 ";FIX$(Y9,4,-11)
AT 1,3 :PRINT "X Tang.2 ";FIX$(X10,4,-11)
AT 1,4 :PRINT "Y Tang.2 ";FIX$(Y10,4,-11)
GET :CLS :BEEP 50,200
AT 1,1 :PRINT "X Cercle ";FIX$(X8,4,-11)
AT 1,2 :PRINT "Y Cercle ";FIX$(Y8,4,-11)
AT 1,4 :PRINT "Rayon    ";FIX$(RAY,4,-11)
AT 1,3 :PRINT "Som.->Cercl";FIX$(SC,4,-9)
GET :CLS :BEEP 50,200
AT 1,1 :PRINT "Bissectr.";FIX$(B,4,-11)
AT 1,2 :PRINT "Corde    ";FIX$(C,4,-11)
AT 1,3 :PRINT "Fleche   ";FIX$(FL,4,-11)
AT 1,4 :PRINT "Develop. ";FIX$(DEV,4,-11)
get
BEEP 50,200
cls :AT 1,1 :PRINT "RACCORD.  CIRCULAIRE"
AT 1,2 :PRINT "--------------------"
AT 1,3 :PRINT "UN AUTRE CALCUL O/N"
LABEL::
T%=GET
IF T%=%O :GOTO MENU::
ELSEIF T%=%N :RETURN
ELSE :GOTO LABEL::
ENDIF
MENU:: :CLS :BEEP 50,200
AT 1,1 :PRINT "RACCORD.  CIRCULAIRE"
AT 1,2 :PRINT "--------------------"
M%=MENUN(2,"DROITE2,CERCLE,NOUVEAU,FIN")
IF M%=0 OR M%=4 :RETURN
ELSEIF M%=1 :GOTO D2::
ELSEIF M%=2 :GOTO CERCLE::
ELSEIF M%=3 :GOTO DEBUT::
ELSE :GOTO LABEL::
ENDIF

