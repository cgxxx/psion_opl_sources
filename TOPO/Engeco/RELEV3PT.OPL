relev3pt:
REM *RELEVEMENT 3 PTS*
REM * DGX ** AVRIL 90*
LOCAL A,B,C,C1,C2,G1,G2,AFI,FI,XY,YX,YX1,X1,X2,X3,Y1,Y2,Y3,X,Y,L1,L2,L3,DX1,DY1,DX2,DY2,D1,D2,D3,D4,Z,Z1,DXS,DYS,XST,YST,VST,DIST1,DIST2,DIST3,DXX1,DXX2,DXX3,DYY1,DYY2,DYY3,GIST,T%,M%
IF PEEKB($2099)<>255 :RETURN :ENDIF
AT 1,1 :PRINT "CALCUL DE RELEVEMENT"
AT 3,2 :PRINT "SUR TROIS POINTS"
AT 3,3 :PRINT "METHODE DE GAUSS"
AT 1,4 :PRINT "PROBLEME DE POTHENOT"
PAUSE 60
DEB:: :CLS
PRINT "X DU PT 1 :"; :INPUT X1 :IF X1=0 :GOTO SUITE:: :ENDIF
PRINT "Y du PT 1 :"; :INPUT Y1
PRINT "Lect 1 -> :"; :INPUT L1
LECT2:: :CLS
AT 1,1 :PRINT "X DU PT 2 :"; :input X2 :IF X2=0 :GOTO SUITE:: :ENDIF
PRINT "Y DU PT 2 :"; :INPUT Y2
PRINT "Lect.2 -> :"; :INPUT L2
LECT3:: :CLS
PRINT "X DU PT 3 :"; :INPUT X3 :IF X3=0 :GOTO SUITE:: :ENDIF
PRINT "Y DU PT 3 :"; :INPUT Y3
PRINT "Lect.3 -> :"; :INPUT L3
A=L2-L1 :B=L3-L2
IF A<0 :A=400+A :ENDIF
IF B<0 :B=400+B :ENDIF
A=A*PI/200 :B=B*PI/200
DX1=X2-X1 :DX2=X2-X3
DY1=Y2-Y1 :DY2=Y2-Y3
IF DY1=0 :DY1=.000000000001 :ENDIF
IF DY2=0 :DY2=.000000000001 :ENDIF
D1=SQR((DX1*DX1)+(DY1*DY1))
D2=SQR((DX2*DX2)+(DY2*DY2))
G1=ATAN(DX1/DY1)
G2=ATAN(DX2/DY2)
G1=G1/PI*200 :IF DY1>0 :G1=G1+200 :ELSEIF DX1>0 :G1=G1+400 :ENDIF :G1=G1*PI/200
G2=G2/PI*200 :IF DY2>0 :G2=G2+200 :ELSEIF DX2>0 :G2=G2+400 :ENDIF :G2=G2*PI/200
C=G1-G2
XY=(400*PI/200)-(A+B+C)
CLS :PRINT CHR$(16);
AT 1,1 :PRINT "Distance Pt1 <-> Pt2"
AT 4,2 :PRINT FIX$(D1,4,-10)
AT 1,3 :PRINT "Distance Pt2 <-> Pt3"
at 4,4 :PRINT FIX$(D2,4,-10)
PAUSE 70
PRINT CHR$(16);
CLS :AT 2,2 :PRINT "PATIENCE ...." :PRINT " CALCULS EN COURS.."
AFI=D1*SIN(B)
AFI=AFI/(D2*SIN(A))
FI=ATAN(AFI)
YX1=TAN(XY/2)
Z=50*PI/200 :Z1=TAN(Z-FI)
YX1=YX1*Z1
YX=ATAN(YX1)*2
X=(XY+YX)/2
Y=XY-X
C1=PI-(A+X)
C2=PI-(B+Y)
D3=D1*SIN(X)
D3=D3/SIN(A)
D4=(D2*SIN(Y))/SIN(B)
DXS=SIN(G1-C1)*(D3+D4)/2
DYS=COS(G1-C1)*(D3+D4)/2
XST=X2+DXS :YST=Y2+DYS
REM CALCUL V0 POND. :
DXX1=XST-X1 :DXX2=XST-X2 :DXX3=XST-X3
DYY1=YST-Y1 :DYY2=YST-Y2 :DYY3=YST-Y3
IF DYY1=0 :DYY1=.000000000001 :ENDIF
IF DYY2=0 :DYY2=.000000000001 :ENDIF
IF DYY3=0 :DYY3=.000000000001 :ENDIF
DIST1=SQR((DXX1*DXX1)+(DYY1*DYY1))
DIST2=SQR((DXX2*DXX2)+(DYY2*DYY2))
DIST3=SQR((DXX3*DXX3)+(DYY3*DYY3))
GIST=ATAN(DXX2/DYY2)
GIST=GIST/PI*200
IF DYY2>0 :GIST=GIST+200 :ELSEIF DXX2>0 :GIST=GIST+400 :ENDIF
VST=GIST-L2 :IF VST<0 :VST=VST+400 :ELSEIF VST>=400 :VST=VST-400 :ENDIF 
CLS :PRINT CHR$(16);
AT 1,1 :PRINT "DISTANCES CALCULEES"
PRINT "DIST. 1 :";FIX$(DIST1,4,-10)
PRINT "DIST. 2 :";FIX$(DIST2,4,-10)
PRINT "DIST. 3 :";FIX$(DIST3,4,-10)
GET
CLS :PRINT CHR$(16);
AT 1,1 :PRINT "X POLE :";FIX$(XST,4,-12)
AT 1,2 :PRINT "Y POLE :";FIX$(YST,4,-12)
AT 1,3 :PRINT "V0 POLE: ";FIX$(VST,4,-11)
PAUSE 30 :AT 1,4 :PRINT CHR$(16);
AT 1,4 :PRINT "UN AUTRE CALCUL O/N"
LABEL::
T%=GET
IF T%=%O :GOTO SUITE::
ELSEIF T%=%N :return
ELSE  :GOTO LABEL:: :ENDIF
SUITE:: :CLS
PRINT "RELEV. sur 3 POINTS"
PRINT "--------------------"
M%=MENUN(2,"POINT2,POINT3,NOUVEAU,FIN")
IF M%=0 OR M%=4 :return
ELSEIF M%=1 :GOTO LECT2::
ELSEIF M%=2 :GOTO LECT3::
ELSEIF M%=3 :GOTO DEB:: 
ENDIF
