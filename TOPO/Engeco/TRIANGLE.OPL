triangle:
REM ** RESOLUTION DE TRIANGLES **
REM ** DGX * 19 JUILLET 90 *
LOCAL A,A1,B,B1,C,C1,A(3),AA(3),C(3),P,T%,M%,X,Y
IF PEEKB($2099)<>255 :RETURN :ENDIF
DEB:: :CLS
DO
AT 1,1 :PRINT "RESOLUTION  TRIANGLE"
AT 1,2 :PRINT REPT$("-",20);
M%=MENUN(2,"1 COTE +2 ANGLES ADJ,COTE+ANGL AD+ANGL OP,1 ANGLE +2 COTES ADJ,ANGL+COTE AD+COTE OP,TROIS COTES,QUITTER")
IF M%=6 :ONERR OFF :return
ELSEIF M%=1 :GOTO C2AA::
ELSEIF M%=2 :GOTO CADAO::
ELSEIF M%=3 :GOTO A2CA::
ELSEIF M%=4 :GOTO ACACO::
ELSEIF M%=5 :GOTO CCC::
ENDIF
UNTIL 0
C2AA:: :CLS
ONERR ERR::
AT 1,1 :PRINT "1 COTE/ 2 ANGLES ADJ."
AT 1,2 :PRINT "ANGLE No 1";chr$(63);" "; :INPUT A(1) :IF A(1)=0 :GOTO DEB:: :ENDIF
PRINT "ANGLE No 2";chr$(63);" "; :input A(2) 
IF A(1)+A(2)>200 :BEEP 50,100 :CLS :AT 1,2 :PRINT" SOMME DES 2 ANGLES   SUPERIEURE A 200": PAUSE 50 :GOTO C2AA:: : ENDIF
PRINT "COTE ";CHR$(63);" "; :INPUT C(3)
AA(1)=A(1)*PI/200
AA(2)=A(2)*PI/200
AA(3)=(200*(PI/200))-(AA(1)+AA(2))
COM:: 
ONERR ERR::
C(2)=SIN(AA(2))*C(3)/SIN(AA(3))
C(1)=SIN(AA(1))*C(3)/SIN(AA(3))
A(3)=AA(3)/PI*200
GOTO AF::
CADAO:: :CLS
ONERR ERR::
AT 1,1 :PRINT "COTE+ANGL AD+ANGL OP"
AT 1,2 :PRINT "COTE ";CHR$(63);" "; :INPUT C(3) :IF C(3)=0 :GOTO DEB:: :ENDIF
PRINT "ANGLE OPP. ";CHR$(63);" "; :INPUT A(3)
PRINT "ANGLE ADJ. ";CHR$(63);" "; :INPUT A(1)
IF A(1)+A(3)>200 :BEEP 50,100 :CLS :AT 1,2 :PRINT" SOMME DES 2 ANGLES   SUPERIEURE A 200" :PAUSE 50 :GOTO CADAO:: :ENDIF
AA(1)=A(1)*PI/200
AA(3)=A(3)*PI/200
AA(2)=(200*(PI/200))-(AA(1)+AA(3))
A(2)=AA(2)/PI*200
GOTO COM::
A2CA:: :CLS
ONERR ERR::
AT 1,1 :PRINT "1 ANGLE +2 COTES ADJ"
AT 1,2 :PRINT "ANGLE ";CHR$(63);" "; :INPUT A(2) :IF A(2)=0 :GOTO DEB:: :ENDIF
PRINT "COTE No 1 ";chr$(63);" "; :INPUT C(1)
PRINT "COTE No 2 ";CHR$(63);" "; :INPUT C(3)
AA(2)=A(2)*PI/200
Y=PI-AA(2)
X=ATAN((C(1)-C(3))*TAN(Y/2)/(C(1)+C(3)))
AA(3)=(Y-2*X)/2
AA(1)=(Y+2*X)/2
C(2)=C(1)*SIN(AA(2))/SIN(AA(1))
A(1)=AA(1)/PI*200
A(3)=AA(3)/PI*200
GOTO AF::
ACACO:: :CLS
ONERR ERR::
AT 1,1 :PRINT "ANGL+COTE AD+COTE OP"
AT 1,2 :PRINT "ANGLE ";CHR$(63);" "; :INPUT A(1) :IF A(1)=0 :GOTO DEB:: :ENDIF
PRINT "COTE OPP.";CHR$(63);" "; :INPUT C(1)
PRINT "COTE ADJ. ";CHR$(63);" "; :INPUT C(2)
AA(1)=A(1)*PI/200
AA(2)=ASIN(C(2)*SIN(AA(1))/C(1))
AA(3)=PI-(AA(1)+AA(2))
C(3)=SIN(AA(3))*C(1)/SIN(AA(1))
A(2)=AA(2)/PI*200
A(3)=AA(3)/PI*200
GOTO AF::
CCC:: :CLS
ONERR ERR::
AT 4,1 :PRINT "** 3 COTES **"
AT 1,2 :PRINT "COTE No 1";chr$(63);" "; :INPUT C(1) :IF C(1)=0 :GOTO DEB:: :ENDIF
PRINT "COTE No 2";chr$(63);" "; :input C(2) 
PRINT "COTE No 3";CHR$(63);" "; :INPUT C(3)
IF C(1)+C(2)<C(3) OR C(2)+C(3)<C(1) OR C(1)+C(3)<C(2) :BEEP 50,100 :CLS :PRINT "  LA SOMME DE DEUX  COTES EST INFERIEURE     AU 3e COTE" :PAUSE 50 :GOTO CCC:: :ENDIF
P=(C(1)+C(2)+C(3))/2
A=(P-C(2)) :A=A*(P-C(3)) :A1=P*(P-C(1)) :A=A/A1 :A=SQR(A) :A(1)=2*ATAN(A)
B=(P-C(1)) :B=B*(P-C(3)) :B1=P*(P-C(2)) :B=B/B1 :B=SQR(B) :A(2)=2*ATAN(B)
C=(P-C(1)) :C=C*(P-C(2)) :C1=P*(P-C(3)) :C=C/C1 :C=SQR(C) :A(3)=2*ATAN(C)
A(1)=A(1)/PI*200 :A(2)=A(2)/PI*200 :A(3)=A(3)/PI*200

AF:: :CLS
IF C(1)=0 OR C(2)=0 OR C(3)=0 OR A(1)<=0 OR A(2)<=0 OR A(3)<=0 :GOTO ERR:: :ENDIF
BEEP 50,100
AT 1,1 :PRINT "A=";FIX$(A(1),4,-8)
PRINT "A=";FIX$(A(2),4,-8)
PRINT "A=";FIX$(A(3),4,-8)
AT 12,1 :PRINT "C=";FIX$(C(1),3,-7)
AT 12,2 :PRINT "C=";FIX$(C(2),3,-7)
AT 12,3 :PRINT "C=";FIX$(C(3),3,-7)
AT 1,4 :PRINT "UN AUTRE CALCUL  O/N"
LAB::
T%=GET
IF T%=%O :GOTO DEB::
ELSEIF T%=%N :return
ELSE :GOTO LAB::
ENDIF
ERR:: :ONERR OFF
BEEP 50,100 :CLS :AT 5,2 :PRINT "IMPOSSIBLE." :PAUSE 50 :GOTO DEB::
