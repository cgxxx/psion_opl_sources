XY:
REM **** X/Y ****
REM ** DGX ** FEV.90 **
LOCAL X1,X2,Y1,Y2,DX,DY,DIST,GIS,GIS1,T%,M%,DSP,ZEN,ZEN1,V
IF PEEKB($2099)<>255 :RETURN :ENDIF
DEBUT:: :CLS
X2=Y2=0
AT 3,1 :PRINT "COORD. X DU POLE ";CHR$(63); :AT 7,2 :INPUT X1
IF X1=0 :GOTO CHEPOLE:: :ENDIF
AT 3,3 :PRINT "COORD. Y DU POLE ";CHR$(63) :AT 7,4 :INPUT Y1
POLE:: :CLS
AT 1,1 :PRINT "X POLE :";FIX$(X1,3,-11)
AT 1,2 :PRINT "Y POLE :";FIX$(Y1,3,-11)
AT 1,3 :PRINT "V0 POLE ";CHR$(63);" "; :INPUT V
AT 1,3 :PRINT "Dist. si SP 0";CHR$(63);" "; :INPUT DIST
IF DIST=0 
AT 1,3 :PRINT"Distance SP ";CHR$(63); :INPUT DSP
AT 1,4 :PRINT "Zenithal ";CHR$(63);" "; :INPUT zen1
ZEN1=300-ZEN1 
ZEN=ZEN1*PI/200
DIST=ABS(DSP*(COS(ZEN)))
ENDIF
AT 1,3 :PRINT REPT$(" ",20)
AT 1,3 :PRINT "Distance : ";FIX$(DIST,3,-8)
AT 1,4 :PRINT "Azimuth  :";CHR$(63);" "; :INPUT GIS
GIS=GIS+V
GIS1=GIS*PI/200
DX=SIN(GIS1)*DIST
DY=COS(GIS1)*DIST
X2=X1+DX :Y2=Y1+DY
CLS
AT 6,1 :PRINT "*** X.Y ***"
AT 1,2 :PRINT "X POINT :";fix$(X2,3,-11)
AT 1,3 :PRINT "Y POINT :";fix$(Y2,3,-11)
AT 1,4 :PRINT CHR$(16);
PAUSE 50
AT 1,4 :PRINT CHR$(16);
AT 1,4 :PRINT "UN AUTRE CALCUL O/N"
LABEL::
T%=GET
IF T%=%O :GOTO CHEPOLE::
ELSEIF T%=%N :TOPO:
ELSE :GOTO LABEL::
ENDIF
CHEPOLE:: :CLS
PRINT "    *** X/Y ***"
PRINT "--------------------"
M%=MENUN(2,"POLE,CHEMIN.,NOUVEAU,FIN")
IF M%=0 OR M%=4 :TOPO:
ELSEIF M%=1 :GOTO POLE::
ELSEIF M%=2 :X1=X2 :Y1=Y2 :X2=Y2=0 :GOTO POLE::
ELSEIF M%=3 :GOTO DEBUT::
ENDIF
